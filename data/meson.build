settings_schemas = [ 'io.github.mibi88.MibiMdEditor.gschema.xml' ]

install_data(settings_schemas, install_dir: schemas_dir)

gnome.compile_schemas(
    depend_files: files(settings_schemas))
mibimdeditor_sources = gnome.compile_resources(
    'mibimdeditor-resources',
    'gresource.xml',
    source_dir: 'data',
)

meson.add_install_script(find_program('glib-compile-schemas'), schemas_dir)

desktop_file = i18n.merge_file(
        input: 'io.github.mibi88.MibiMdEditor.desktop.in',
       output: 'io.github.mibi88.MibiMdEditor.desktop',
         type: 'desktop',
       po_dir: '../po',
      install: true,
  install_dir: join_paths(get_option('datadir'), 'applications')
)

desktop_utils = find_program('desktop-file-validate', required: false)
if desktop_utils.found()
  test('Validate desktop file', desktop_utils, args: [desktop_file])
endif

appstream_file = i18n.merge_file(
        input: 'io.github.mibi88.MibiMdEditor.appdata.xml.in',
       output: 'io.github.mibi88.MibiMdEditor.appdata.xml',
       po_dir: '../po',
      install: true,
  install_dir: join_paths(get_option('datadir'), 'appdata')
)

appstream_util = find_program('appstream-util', required: false)
if appstream_util.found()
  test('Validate appstream file', appstream_util, args: ['validate', appstream_file])
endif

subdir('icons')
